#!/usr/bin/env perl

use strict;
use warnings FATAL => 'all';

use English qw(-no_match_vars);

my %headings = (
   '1' => '=',
   '2' => '-',
);
my $list   = 0;

# Skip up until the documentation starts.
while(<>) {
   last if m/^=pod/;
}

# Read a para at a time
$RS = "";

LINE:
while (<>) {
   my $line = $_;
   chomp $line;

   if ( $line eq '=cut' ) {
      last LINE;
   }

   # Headings
   if ( my ($level, $val) = $line =~ m/^=head(\d)\s+(.*)$/ ) {
      print $val, "\n";
      $val =~ s/./$headings{$level}/g;
      print $val, "\n\n";
      next LINE;
   }

   if ( $line =~ m/^(\s+)/ ) {
      print ".. code-block::\n", $line, "\n\n";
      next LINE;
   }

   if ( $line =~ m/=over/ ) {
      $list = 1;
      next LINE;
   }
   elsif ( $line =~ m/=back/ ) {
      $list = 0;
      next LINE;
   }

   if ( $list ) {
      if ( $line =~ m/^=item */ ) {
         print "*";
         next LINE;
      }
      else {
         $line =~ s/^/   /gm;
         $line =~ s/^ //;
         print $line, "\n\n";
         next LINE;
      }
   }

   print $line, "\n\n";
}
